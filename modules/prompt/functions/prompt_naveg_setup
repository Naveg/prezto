#
# A simple theme that only shows relevant information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Evan Goldenberg <evangoldenberg@gmail.com>
#

function prompt_naveg_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_naveg_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_naveg_precmd

  zstyle ':prezto:module:editor:info:completing' format '%F{red}...%f'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %F{yellow}❮%f%F{red}❮%f%F{red}❮%f'
  zstyle ':prezto:module:git:info:action' format ':%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ' %F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{green}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt'  '%b%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  PROMPT='%F{magenta}[%m]%F{green}[%1~]%f%(?:: %F{red}⏎ %f) %F{green}❯%f%b '
  RPROMPT='${editor_info[keymap]}${editor_info[overwrite]}${VIM:+" %F{green}V%f%b"}%f${git_info:+${(e)git_info[prompt]}}${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_naveg_setup "$@"


